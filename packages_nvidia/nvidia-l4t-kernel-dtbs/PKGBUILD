# Maintainer: nyped

pkgname=nvidia-l4t-kernel-dtbs
pkgver=4.9.337.tegra.32.7.4.20230608212426
pkgrel=1
pkgdesc="TODO"
arch=('aarch64')
url="https://repo.download.nvidia.com"
license=('custom')
depends=('nvidia-l4t-core' 'nvidia-l4t-kernel' 'dtc')
makedepends=('tar')
options=('!strip')
source=(
  https://repo.download.nvidia.com/jetson/t210/pool/main/n/nvidia-l4t-kernel-dtbs/nvidia-l4t-kernel-dtbs_4.9.337-tegra-32.7.4-20230608212426_arm64.deb
)
sha512sums=(
  4fe10c9578fcac2bfd5083eedb9f091699b162c9ca6a287702f44a7ec6317beb7f92ced2a529a71ae94ed34d1a11479262fb9346fed9ef2434f2a34a6547ca42
)

_mv() {
  source="${pkgdir}/$1" dest="${pkgdir}/$2"

  if [[ -d "$source" ]] # $1 is a dir
  then
    # moving everything
    rmdir "$source" 2>/dev/null || {
      mkdir -p "$dest"
      mv "$source"/* "$dest"
      rmdir "$source"
    }
  elif [[ -f "$source" ]] # $1 is a file
  then
    # moving the file
    mv "$source" "$dest"
  fi
}

package() {
  # Unpack the deb file
  tar xf data.tar.* --keep-directory-symlink -C "${pkgdir}"

  # Restore symlinks
  _mv /lib /usr/lib
  _mv /usr/sbin /usr/bin

  # Install licenses properly
  _mv /usr/share/doc/$pkgname /usr/share/licenses/$pkgname
  _mv /usr/share/doc/nvidia-tegra /usr/share/licenses/$pkgname

  # Setup permission
  find "$pkgdir" \( -name \*.so -o -name \*.so.\* -o -name \*.bin -o -name \*.fw \) -exec chmod 755 '{}' ';'
}

# vim: set ts=2 sts=2 sw=2 ft=sh et :
