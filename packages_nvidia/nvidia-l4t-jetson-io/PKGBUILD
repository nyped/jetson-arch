# Maintainer: nyped

pkgname=nvidia-l4t-jetson-io
pkgver=32.7.4.20230608212426
pkgrel=1
pkgdesc="TODO"
arch=('aarch64')
url="https://repo.download.nvidia.com"
license=('custom')
depends=('nvidia-l4t-core' 'nvidia-l4t-kernel' 'util-linux' 'python' 'dtc')
makedepends=('tar')
options=('!strip')
source=(
  https://repo.download.nvidia.com/jetson/t210/pool/main/n/nvidia-l4t-jetson-io/nvidia-l4t-jetson-io_32.7.4-20230608212426_arm64.deb
)
sha512sums=(
  818d2190e352d2e203dcd69caa18ff9ccd3b5baad025266d1c738aed721af064f0c6365197ab9f61577680f9900b4707ccae799d733fcdbc63d9d86e12d2c3e0
)

_mv() {
  source="${pkgdir}/$1" dest="${pkgdir}/$2"

  if [[ -d "$source" ]] # $1 is a dir
  then
    # moving everything
    rmdir "$source" 2>/dev/null || {
      mkdir -p "$dest"
      mv "$source"/* "$dest"
      rmdir "$source"
    }
  elif [[ -f "$source" ]] # $1 is a file
  then
    # moving the file
    mv "$source" "$dest"
  fi
}

package() {
  # Unpack the deb file
  tar xf data.tar.* --keep-directory-symlink -C "${pkgdir}"

  # Restore symlinks
  _mv /lib /usr/lib
  _mv /usr/sbin /usr/bin

  # Install licenses properly
  _mv /usr/share/doc/$pkgname /usr/share/licenses/$pkgname
  _mv /usr/share/doc/nvidia-tegra /usr/share/licenses/$pkgname

  # Setup permission
  find "$pkgdir" \( -name \*.so -o -name \*.so.\* -o -name \*.bin -o -name \*.fw \) -exec chmod 755 '{}' ';'
}

# vim: set ts=2 sts=2 sw=2 ft=sh et :
