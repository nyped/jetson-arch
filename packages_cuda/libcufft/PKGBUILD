# Maintainer: nyped

pkgname=libcufft
pkgver=10.1.2.300
pkgrel=1
pkgdesc="TODO"
arch=('aarch64')
url="https://repo.download.nvidia.com"
license=('custom')
makedepends=('tar')
groups=('nvidia-cuda')
options=('!strip')
cuda_version=10.2
source=(
  https://repo.download.nvidia.com/jetson/common/pool/main/libc/libcufft/libcufft-10-2_10.1.2.300-1_arm64.deb
)
sha512sums=(
  172a0ebc98fc12bbd1a4e35a92bd0c185b0fc6f2e67e0503f7db5bb9d76fbc3c8fb35d67855484b01b30cf8b75e7a6e5020002daaf2ab1584bf376b2444916fb
)

_mv() {
  source="${pkgdir}/$1" dest="${pkgdir}/$2"

  if [[ -d "$source" ]] # $1 is a dir
  then
    # moving everything
    rmdir "$source" 2>/dev/null || {
      mkdir -p "$dest"
      mv "$source"/* "$dest"
      rmdir "$source"
    }
  elif [[ -f "$source" ]] # $1 is a file
  then
    # moving the file
    mv "$source" "$dest"
  fi
}

package() {
  # Unpack the deb file
  tar xf data.tar.* --keep-directory-symlink -C "${pkgdir}"

  # Restore symlinks
  _mv /lib /usr/lib
  _mv /usr/sbin /usr/bin

  # Install licenses properly
  _mv /usr/share/doc /usr/share/licenses/$pkgname

  # Setup permission
  find "$pkgdir" \( -name \*.so -o -name \*.so.\* -o -name \*.bin -o -name \*.fw \) -exec chmod 755 '{}' ';'

  # Remove useless links
  rm -f "$pkgdir"/usr/local/cuda-${cuda_version}/{include,lib64}
}

# vim: set ts=2 sts=2 sw=2 ft=sh et :
