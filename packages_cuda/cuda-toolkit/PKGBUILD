# Maintainer: nyped

pkgname=cuda-toolkit
pkgver=10.2.460
pkgrel=1
pkgdesc="TODO"
arch=('aarch64')
url="https://repo.download.nvidia.com"
license=('custom')
makedepends=('tar')
groups=('nvidia-cuda')
options=('!strip')
cuda_version=10.2
source=(
  https://repo.download.nvidia.com/jetson/common/pool/main/c/cuda-toolkit-10-2/cuda-toolkit-10-2_10.2.460-1_arm64.deb
)
sha512sums=(
  d94c02b34290360016b7d66f513de8954943b287daaa3c6e65808e4eed48b423da931fada0684effe8198bc9f53ddfd847fce03cf604c4d3468df035ea34f899
)

_mv() {
  source="${pkgdir}/$1" dest="${pkgdir}/$2"

  if [[ -d "$source" ]] # $1 is a dir
  then
    # moving everything
    rmdir "$source" 2>/dev/null || {
      mkdir -p "$dest"
      mv "$source"/* "$dest"
      rmdir "$source"
    }
  elif [[ -f "$source" ]] # $1 is a file
  then
    # moving the file
    mv "$source" "$dest"
  fi
}

package() {
  # Unpack the deb file
  tar xf data.tar.* --keep-directory-symlink -C "${pkgdir}"

  # Restore symlinks
  _mv /lib /usr/lib
  _mv /usr/sbin /usr/bin

  # Install licenses properly
  _mv /usr/share/doc /usr/share/licenses/$pkgname

  # Setup permission
  find "$pkgdir" \( -name \*.so -o -name \*.so.\* -o -name \*.bin -o -name \*.fw \) -exec chmod 755 '{}' ';'

  # Remove useless links
  rm -f "$pkgdir"/usr/local/cuda-${cuda_version}/{include,lib64}
}

# vim: set ts=2 sts=2 sw=2 ft=sh et :
